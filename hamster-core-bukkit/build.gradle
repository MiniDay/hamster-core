setArchivesBaseName("HamsterCore-Bukkit")

evaluationDependsOn(':hamster-core-common')

configurations {
    api.extendsFrom apiShade
    implementation.extendsFrom implementationShade
}

dependencies {
    compileOnly project(":hamster-core-common")
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'

    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    //noinspection GradlePackageUpdate
    apiShade 'com.zaxxer:HikariCP:4.0.3'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    apiShade 'com.squareup.okhttp3:okhttp:4.10.0'
    // https://mvnrepository.com/artifact/net.kyori/adventure-platform-bukkit
    apiShade 'net.kyori:adventure-platform-bukkit:4.1.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

processResources {
    inputs.property "version", project.version
    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}

task addSource {
    doLast {
        sourceSets {
            main {
                java.srcDirs += [
                        project(':hamster-core-common').sourceSets.main.java
                ]
                resources.srcDirs += [
                        project(':hamster-core-common').sourceSets.main.resources
                ]
            }
        }
    }
}
classes.dependsOn(addSource)

jar {
    from([
            configurations.implementationShade.collect {
                it.isDirectory() ? it : zipTree(it)
            },
            configurations.apiShade.collect {
                it.isDirectory() ? it : zipTree(it)
            }
    ])
}
