setArchivesBaseName("HamsterCore-Bukkit")

evaluationDependsOn(':hamster-core-common')

dependencies {
    apiShade(project(":hamster-core-common")) { transitive = false }

    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    // https://mvnrepository.com/artifact/net.kyori/adventure-platform-bukkit
    apiShade 'net.kyori:adventure-platform-bukkit:4.1.2'

    compileOnly('net.milkbowl.vault:VaultAPI:1.7') { transitive = false }
    compileOnly('org.black_ixx:playerpoints:2.1.3') { transitive = false }

    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    //noinspection GradlePackageUpdate
    apiShade 'com.zaxxer:HikariCP:4.0.3'
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    shade 'mysql:mysql-connector-java:8.0.31'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    apiShade 'com.squareup.okhttp3:okhttp:4.10.0'
}

processResources {
    inputs.property "version", project.version
    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}

tasks.compileJava.dependsOn(":hamster-core-common:build")
tasks.create("shadowJar", Jar) {
    dependsOn("jar")
    from([
            tasks.jar.outputs.files.collect {
                it.isDirectory() ? it : zipTree(it)
            },
            configurations.shade.collect {
                it.isDirectory() ? it : zipTree(it)
            },
            configurations.apiShade.collect {
                it.isDirectory() ? it : zipTree(it)
            },
            configurations.implementationShade.collect {
                it.isDirectory() ? it : zipTree(it)
            }
    ])
    destinationDir(getRootProject().buildDir)
}
tasks.build.dependsOn(shadowJar)
